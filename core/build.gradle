plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
}

group buildConfig.groupId

apply from: rootProject.file('gradle/export-test-helpers-workaround.gradle')

android {
    namespace = 'com.nabla.sdk.core'

    compileSdk buildConfig.compileSdk

    defaultConfig {
        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionName getGitDescribe()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro', 'serialization-rules.pro'

        aarMetadata {
            minCompileSdk buildConfig.compileSdk
        }

        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""
    }

    compileOptions {
        sourceCompatibility buildConfig.jvmTarget
        targetCompatibility buildConfig.jvmTarget
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'VisibleForTests'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }
}

String getGitDescribe() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    // we don't want it for test src, see https://stackoverflow.com/questions/64267076
    if (!it.name.contains("Test")) {
        kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}

dependencies {
    implementation libs.okhttp
    implementation libs.okhttp.logging
    implementation libs.retrofit
    implementation libs.retrofit.kotlin.serialization
    implementation libs.jwtdecode
    implementation libs.apollo.runtime
    implementation libs.apollo.cache
    implementation libs.apollo.cache.sqlite
    implementation libs.apollo.adapters
    implementation libs.androidx.startup
    implementation libs.kotlin.serialization.json
    api libs.kotlin.datetime
    api libs.uuid

    testImplementation libs.junit
    testImplementation libs.turbine
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockk
    testImplementation libs.apollo.testingSupport
    testImplementation libs.kotlin.test
    testImplementation libs.roboelectric
    testImplementation libs.android.junit
    testImplementation project(':messaging-core-test-fixtures')
    testImplementation libs.espressoIdlingResource
}
