
/**
 * Since testFixtures is currently not supported by kotlin compiler (only java),
 * Using the workaround from https://issuetracker.google.com/issues/139438142#comment15
 ****
 *
 * Creates a new configuration and publishes a test utility JAR so you can depend on it from other modules.
 * For example:
 * testImplementation project(path: ':feature:other-module', configuration: 'test-helpers')
 *
 * To export test-helpers JAR from any com.android.library module, add to its build.gradle:
 * apply from: rootProject.file('gradle/export-test-helpers-workaround.gradle')
 */

task jarTestHelpers(type: Jar, dependsOn: 'compileDebugUnitTestKotlin') {
    from "$buildDir/intermediates/javac/debugUnitTest/classes"
    from "$buildDir/tmp/kotlin-classes/debugUnitTest"

    // exclude classes ending in 'Test' or 'TestKt' and any inner or anonymous classes
    exclude '**/*Test.class'
    exclude '**/*TestKt.class'
    exclude '**/*Test_*.class'
    exclude '**/*Test$*.class'
    exclude '**/*TestKt$*.class'

    // as of AGP 3.4+ R classes are generated within intermediates/javac/debugUnitTest/compileDebugUnitTestJavaWithJavac/classes
    // when using android.testOptions.unitTests.includeAndroidResources = true, this causes conflicts with merging of resources
    // the ids in these R files are different to those in the app/androidTest apk
    exclude '**/R.class'
    exclude '**/R$*.class'

    includeEmptyDirs false
    classifier = 'test-helpers'
}

configurations {
    maybeCreate 'test-helpers'
}

artifacts {
    add 'test-helpers', jarTestHelpers
}
